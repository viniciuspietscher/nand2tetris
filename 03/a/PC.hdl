// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in=loop, out=incOut);

    Mux16(a=loop, b=incOut, sel=inc, out=mux0Out);
    Mux16(a=mux0Out, b=in, sel=load, out=mux1Out);
    Mux16(a=mux1Out, b=false, sel=reset, out=mux2Out);

    Register(in=mux2Out, load=true, out=loop, out=out);
}
